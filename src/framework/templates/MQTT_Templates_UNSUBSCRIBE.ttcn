/*******************************************************************************
 * Copyright (c) 2018 Relayr GmbH
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   Alexander Kaiser
 *   Sascha Hackel
 *   Axel Rennoch
 *******************************************************************************/

 /*
  * This module contains all templates related to UNSUBSCRIBE control packets
  */
module MQTT_Templates_UNSUBSCRIBE
{
  import from MQTT_v3_1_1_Types all;

  /*
   * Sending Templates are templates without any wildcards or matching expressions.
   * Thus, this templates can be used to create concrete MQTT PDUs with valueof(T)
   */
  group SendingTemplates
  {
    /*
    * UNSUBSCRIBE Control Packet
    *
    * @reference http://docs.oasis-open.org/mqtt/mqtt/v3.1.1/os/mqtt-v3.1.1-os.html#_Toc398718072
    */
    group UNSUBSCRIBE_Group
    {
      group FixedHeaderGroup
      {
        template Header m_unsubscribe_header(BIT4n p_flags := '0010'B) :=
        {
          packetType := '1010'B ,
          flags := p_flags,
          remLength := '00000000'O  // f_calcRemLength will set this field automatically after encoding
        }
      }

      template MQTT_v3_1_1_UnsubscribePayload m_unsubscribe_payload(INT_BIT16_MSB p_len, UCHAR0_65535 p_filter) :=
      {
        filterLength := p_len,
        topic_filter := p_filter
      }

      // Helper functions to retrieve MQTT_v3_1_1_UnsubscribePayload without calculating the filterLength manually
      function ft_unsubscribe_payload(charstring p_in) return template MQTT_v3_1_1_UnsubscribePayload
      {
        return m_unsubscribe_payload(lengthof(p_in), p_in)
      }

      function ft_unsubscribe_payload_list(charstring p_in_1, charstring p_in_2) return template MQTT_v3_1_1_UnsubscribePayloadList
      {
        var template MQTT_v3_1_1_UnsubscribePayload v_p_1 := ft_unsubscribe_payload(p_in_1)
        var template MQTT_v3_1_1_UnsubscribePayload v_p_2 := ft_unsubscribe_payload(p_in_2)
        return m_unsubscribe_payload_list_2(v_p_1, v_p_2)
      }

      template MQTT_v3_1_1_UnsubscribePayloadList m_unsubscribe_payload_list_1(MQTT_v3_1_1_UnsubscribePayload p_payload) :=
      {
        p_payload
      }

      template MQTT_v3_1_1_UnsubscribePayloadList m_unsubscribe_payload_list_2(template MQTT_v3_1_1_UnsubscribePayload p_elem_1, template MQTT_v3_1_1_UnsubscribePayload p_elem_2) :=
      {
        p_elem_1, p_elem_2
      }

      group UNSUBSCRIBE_MessageGroup
      {
        template MQTT_v3_1_1_Message m_unsubscribe(integer p_packet_id, MQTT_v3_1_1_UnsubscribePayloadList p_payload_list) :=
        {
          msg :=
          {
            unsubscribe :=
            {
              header := m_unsubscribe_header,
              packet_identifier := p_packet_id,
              payload := p_payload_list
            }
          }
        }
      }
    }
  }

  /*
   * Matchin Templates are templates with wildcards or matching expressions.
   * This templates should be only used for matching incoming MQTT PDUs
   */
  group MatchingTemplates
  {
    /*
    * UNSUBSCRIBE Control Packet
    *
    * @reference http://docs.oasis-open.org/mqtt/mqtt/v3.1.1/os/mqtt-v3.1.1-os.html#_Toc398718072
    */
    group UNSUBSCRIBE_Group
    {
      group FixedHeaderGroup
      {
        template Header mw_unsubscribe_header(BIT4n p_flags := '0010'B) :=
        {
          packetType := '1010'B ,
          flags := p_flags,
          remLength := ?
        }
      }

      group UNSUBSCRIBE_MessageGroup
      {
        template MQTT_v3_1_1_Message mw_unsubscribe :=
        {
          msg :=
          {
            unsubscribe := ?
          }
        }

        template MQTT_v3_1_1_Message mw_unsubscribe_valid :=
        {
          msg :=
          {
            unsubscribe :=
            {
              header := mw_unsubscribe_header,
              packet_identifier := ?,
              payload := ?
            }
          }
        }
      }
    }
  }
}
